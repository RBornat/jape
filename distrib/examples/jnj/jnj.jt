/*
        This jape theory file was generated from the
        latex source of the paper -- ``JnJ in Jape'' --
        which describes the way in which the logic of the
        Woodcock/Davies book is encoded in Jape. A full
        explanation of how the Jape encoding
        may be used with the book is given in that paper,
        and a description of using the logic with the X
        implementation of Jape is given in the
        companion paper ``Using JnJ in Unix Jape''.

        $Id$

        (Brought up to date for Jape 6 in January 2003)

        Edit in FONT{lauratt-*}
*/
        FONTS   "Laura"
        SEQUENT IS BAG ° FORMULA
        INITIALISE autoAdditiveLeft true
        PREFIX   350 ∆
        INFIX    50R  ã
        INFIX    100R ä
        INFIX    200L ç
        INFIX    300L Ö
        JUXTFIX  40000
        CLASS FORMULA  p q r s
        CLASS VARIABLE x y z
        CONSTANT false true
        SUBSTFIX 50000 [ p / x ]
        RULE  "Ö-intro"  FROM p AND q INFER pÖq
        RULE  "Ö-elim1"  FROM pÖq INFER p
        RULE  "Ö-elim2"  FROM pÖq INFER q

        RULE  "ç-intro1" FROM p INFER pçq
        RULE  "ç-intro2" FROM q INFER pçq
        RULE  "ç-elim"   FROM pçq AND p°r AND q°r INFER r

        RULE  "ä-intro"  FROM p ° q INFER päq
        RULE  "ä-elim"   FROM päq AND p INFER q

        RULE  "∆-intro"  FROM p ° false INFER ∆ p
        RULE  "∆-elim"   FROM ∆p AND p INFER false

        RULE  "false-intro" FROM ∆p AND p INFER false
        RULE  "false-elim"  FROM ∆p ° false INFER  p
        RULE  "true-intro"  INFER  ° true

        RULE  "ã-intro"  FROM p ä q AND   q ä p INFER p ã q
        RULE  "ã-elim1"  FROM p ã q INFER p ä q
        RULE  "ã-elim2"  FROM p ã q INFER q ä p
        RULE hypothesis(p) INFER p ° p
        RULE cut(p)        FROM  ° p AND   p ° q  INFER   ° q
        RULE thin(p)       FROM  ° q              INFER p ° q
        RULE dup(p)        FROM  p, p ° q         INFER p ° q
        IDENTITY RULE hypothesis
        CUT      RULE cut
        WEAKEN   RULE thin
    INFIX   800R  ñ
    LEFTFIX 10  Ñ : §
    LEFTFIX 10  Ñ : | §
    LEFTFIX 10  å : §
    LEFTFIX 10  å : | §
    CLASS FORMULA a b t u
    BIND x SCOPE p   IN Ñ x:a § p
    BIND x SCOPE p   IN å x:a § p
    BIND x SCOPE p q IN Ñ x:a | p § q
    BIND x SCOPE p q IN å x:a | p § q
    RULE    "Ñ-intro"(OBJECT y) WHERE FRESH y
    FROM    yña ° q[y/x]
    INFER   Ñ x:a § q
    RULE    "Ñ-elim"(t)
    FROM    Ñ x:a § q
    AND     tña
    INFER   q[t/x]
    RULE    "å-intro"(t)
    FROM    tña
    AND     q[t/x]
    INFER   å x:a § q
    RULE    "å-elim"(OBJECT y)
    WHERE   FRESH y
    AND     y NOTIN å x:a § p
    AND     y NOTIN r
    FROM    å x:a § p
    AND     yña, p[y/x] ° r
    INFER   r
    RULE "Ñ|§" IS (Ñ x:a|p§q) ã (Ñ x:a § p ä q)
    RULE "å|§" IS (å x:a|p§q) ã (å x:a § p Ö q)
    TACTIC ElimRule(rule)
    (WHEN
      (LETHYP _p
          (ALT (SEQ      (WITHARGSEL rule) (WITHHYPSEL hypothesis))
               (SEQ  cut (WITHARGSEL rule) (WITHHYPSEL hypothesis))
               (Fail ("%s is not applicable to formula %s", rule, _p))))
      (LETCONC _p
          (ElimRuleBackward rule))
      (Fail ("Select an assumption-like formula before applying %s forwards", rule)))
    TACTIC IntroRule(rule)
    (WHEN  (LETARGSEL _p (SEQ (cut _p) rule))
           (ALT (LETHYP    _p rule)
                rule
                (MESSAGE (rule is not applicable))))
    TACTIC QuantIntroRule(rule)
    (ALT (WITHARGSEL rule)
         (Fail ("%s is not applicable", rule)))
MENU    "Propositional Rules"
    ENTRY   "hypothesis"
    SEPARATOR
    ENTRY    "false-elim"   IS (IntroRule "false-elim")
    SEPARATOR
    ENTRY    "Ö-intro"      IS (IntroRule "Ö-intro")
    TACTIC   "Ö-elim-1"     IS (ElimRule  "Ö-elim1")
    ENTRY    "Ö-elim-2"     IS (ElimRule  "Ö-elim2")
    SEPARATOR
    ENTRY    "ç-intro1"     IS (IntroRule "ç-intro1")
    ENTRY    "ç-intro2"     IS (IntroRule "ç-intro2")
    ENTRY    "ç-elim"       IS (ElimRule "ç-elim")
    SEPARATOR
    ENTRY    "ä-intro"      IS (IntroRule "ä-intro")
    ENTRY    "ä-elim"       IS (ElimRule  "ä-elim")
    SEPARATOR
    ENTRY    "∆-intro"      IS (IntroRule "∆-intro")
    ENTRY    "∆-elim"       IS (ElimRule "∆-elim")
    ENTRY    "false-intro"  IS (ElimRule "false-intro")
    ENTRY    "true-intro"   IS (IntroRule "true-intro")
    SEPARATOR
    ENTRY    "ã-intro"      IS (IntroRule "ã-intro")
    ENTRY    "ã-elim1"      IS (ElimRule "ã-elim1")
    ENTRY    "ã-elim2"      IS (ElimRule "ã-elim2")
END
MENU "Quantifier Rules" IS
    ENTRY "Ñ-intro"   IS QuantIntroRule "Ñ-intro"
    ENTRY "Ñ-elim"    IS ElimRule       "Ñ-elim"
    ENTRY "å-intro"   IS QuantIntroRule "å-intro"
    ENTRY "å-elim"    IS ElimRule       "å-elim"
END
    AUTOMATCH hypothesis
    HYPHIT pçq ° r IS  (ALT (ElimRule "ç-elim")  "ç-elim")
    HYPHIT päq ° r IS  (ALT (ElimRule "ä-elim")  "ä-elim")
    HYPHIT ∆p  ° r IS  (ALT (ElimRule "∆-elim")  "∆-elim")
    HYPHIT Ñx:a§q   ° r IS ElimRule "Ñ-elim"
    HYPHIT åx:a§q   ° r IS ElimRule "å-elim"
    HYPHIT pÖq ° r IS
    (ALT (SEQ  "Ö-elim1"  (hypothesis(pÖq)))
         (SEQ  "Ö-elim2"  (hypothesis(pÖq)))
         (SEQ
               cut "Ö-elim1" (hypothesis (pÖq))
               cut "Ö-elim2" (hypothesis (pÖq))
         ))

    HYPHIT pãq ° r IS
    (ALT (SEQ  "ã-elim1"  (hypothesis(pãq)))
         (SEQ  "ã-elim2"  (hypothesis(pãq)))
         (SEQ
               cut "ã-elim1" (hypothesis (pãq))
               cut "ã-elim2" (hypothesis (pãq))
         ))
    CONCHIT Ñx:a§q   IS QuantIntroRule "Ñ-intro"
    CONCHIT åx:a§q   IS QuantIntroRule "å-intro"
    CONCHIT ∆p       IS "∆-intro"
    CONCHIT true     IS "true-intro"
    CONCHIT päq      IS "ä-intro"
    CONCHIT pãq      IS "ã-intro"
    CONCHIT pÖq      IS "Ö-intro"
    CONCHIT qçr      IS
     (ALT  (SEQ "ç-intro1" hypothesis)
           (SEQ "ç-intro2" hypothesis)
           (MESSAGE (Neither disjunct q nor disjunct r is available)))

    CONCHIT  p ° p IS
    (WHEN (LETHYP _p (WITHHYPSEL hypothesis))
          (Fail ("Please select the hypothesis you want to use")))
    MENU Edit
        RADIOBUTTON displaystyle
             "Sequent-Tree display style"  IS tree
        AND  "Box display style"           IS box
        INITIALLY
             box
        END

        CHECKBOX autoselect
                "Automatic goal selection"
        INITIALLY
                true

        CHECKBOX applyconjectures
                "Unproven theorems may be applied"
        INITIALLY
                false
        RADIOBUTTON ElimRuleBackward
            "Forbid backward use of elimination rules" IS ElimRuleBackwardNo
        AND "Permit backward use of elimination rules" IS ElimRuleBackwardYes
        END

    END /* MENU Edit */

    TACTIC ElimRuleBackwardYes(rule)
           (WITHARGSEL rule)

    TACTIC ElimRuleBackwardNo(rule)
    (Fail ("Select a goal then a non-grey formula before invoking %s",
           rule))
    TACTIC Fail(x) IS
           ((SEQ (ALERT x) FAIL))

    TACTIC FailSEL(x, y) IS
           (WHEN (LETARGSEL _p ((SEQ (ALERT x) FAIL))) ((SEQ (ALERT y) FAIL)))

    TACTIC MESSAGE(m) IS
           (SEQ (ALERT m)
                FAIL)

    TACTIC SKIP(x)    IS (SEQ)

        INFIX 1000L ë
CONJECTUREPANEL "Conjectures" IS
    THEOREMS Propositional(p,q,r,s) ARE
        ∆∆p ° p
    AND ∆(pçq) ° ∆p Ö ∆q
    AND ∆(pÖq) ° ∆p ç ∆q
    AND ° p ç ∆p
    AND	p , ∆p ° false
    AND	p Ö ∆p ° false
    AND p ° qäp
    AND ° pãp
    AND ∆ ∆ p ã p
    AND (päq) ã (∆pçq)
    AND (päq) ã (∆qä∆p)
    AND ° (päq) ç (qäp)
    AND (pÖq ä r) ä (p ä (q ä r))
    AND p ä q ° pÖq ã p
    AND	° p ä (q ä p)
    AND	p ä q, q ä r , p ° r
    AND	p ä q ° (q ä r) ä (p ä r)
    AND	p ä (q ä r), p ä q, p ° r
    AND	p ä q, q ä r ° p ä r
    AND	p ä (q ä r) ° q ä (p ä r)
    AND	p ä (q ä (r ä s)) ° r ä (q ä (p ä s ))
    AND	p ä (q ä r) ° (p ä q) ä (p ä r)
    AND	° (p ä (q ä r)) ä ((p ä q) ä (p ä r))
    AND	(p Ö q) ä r ã  p ä (q ä r)
    AND	q ä r ° (p ç q) ä (p ç r)
    AND true  ã ∆false
    AND ∆true  ã false
    AND pÖtrue ã p
    AND pçtrue ã true
    AND pÖfalse ã false
    AND pçfalse ã p
    AND (päp) ã true
    AND (pätrue) ã true
    AND (trueäp) ã p
    AND (päfalse) ã ∆p
    AND (pÖp) ã p
    AND (pÖq) ã (qÖp)
    AND (pÖ(qÖr)) ã ((pÖq)Ör)
    AND (pçp) ã p
    AND (pçq) ã (qçp)
    AND (pç(qçr)) ã ((pçq)çr)
    AND	p ç (q ç r) ã (p ç q) ç r
    AND	p Ö (q ç r) ã (p Ö q) ç (p Ö r)
    AND	p ç (q Ö r) ã (p ç q) Ö (p ç r)
    AND ∆(pçq) ã ∆p Ö ∆q
    AND ∆(pÖq) ã ∆p ç ∆q
    AND (päq) Ö (qär) ä (pär)
    AND (pãq) Ö (qãr) ä (pãr)
    AND	∆p Ö p ° false
    AND ∆(pãq)ã((pçq)Ö∆(pÖq))
    END
    THEOREMS Predicate
    ARE Ñx:a § pÖq ° (Ñx:a § p) Ö (Ñx:a § q)
    AND (Ñx:a § p) Ö (Ñx:a § q) ° Ñx:a § pÖq
    AND ∆(å x:a§q) ° (Ñ x:a§∆q)
    AND ∆(Ñ x:a§q) ° (å x:a§∆q)
    AND ∆(Ñ x:a|p§q) ° (å x:a|p§∆q)
    AND WHERE x NOTIN a,a',y AND y NOTIN a,a',x INFER
        åx:a § Ñy:a' § q ° Ñy:a' § åx:a § q
    END
    THEOREM "Ñ|§-intro" IS Ñ x:a § p ä q ° Ñ x:a | p § q
    THEOREM "Ñ|§-elim"  IS Ñ x:a | p § q ° Ñ x:a § p ä q
    THEOREM "å|§-intro" IS å x:a § p Ö q ° å x:a | p § q
    THEOREM "å|§-elim"  IS å x:a | p § q ° å x:a § p Ö q
    THEOREM "?-åx:a§Ñy:a'§xëy°Ñy:a'§åx:a§xëy(no provisos)"
    IS å x:a § Ñy:a' § xëy ° Ñy:a' § åx:a § xëy
    THEOREM "?- (päq)äp" IS (päq)äp
    THEOREM "?- (pç∆p)ä∆p" IS (pç∆p)ä∆p
    THEOREM "?- Ñy:a'§åx:a§q ° åx:a§Ñy:a'§q"
    WHERE x NOTIN a,a',y AND y NOTIN a,a',x
    IS Ñy:a'§åx:a§q ° åx:a§Ñy:a'§q
    BUTTON "Apply Forward"   IS apply ThmForward COMMAND
    BUTTON "Apply Backwards" IS apply ThmBackward COMMAND
END /* PANEL */
    TACTIC ThmForward(thm) IS (SEQ cut (WITHHYPSEL (WITHARGSEL thm)))
    TACTIC ThmBackward(thm) IS (WITHARGSEL(APPLYORRESOLVE thm))
        RULE    rewrite(p, OBJECT x)
        FROM    p ã q
        AND     r[q/x]
        INFER   r[p/x]

        RULE    rewritebackwards(p, OBJECT x)
        FROM    q ã p
        AND     r[q/x]
        INFER   r[p/x]

        RULE    hyprewrite(p, OBJECT x)
        FROM    p ã q
        AND     r[q/x] ° s
        INFER   r[p/x] ° s

        RULE    hyprewritebackwards(p, OBJECT x)
        FROM    q ã p
        AND     r[q/x] ° s
        INFER   r[p/x] ° s
    CONJECTUREPANEL Conjectures IS
     BUTTON "Rewrite â" IS
      apply RewriteWith hyprewrite rewrite COMMAND
     BUTTON "Rewrite Å" IS
      apply RewriteWith hyprewritebackwards rewritebackwards COMMAND
    END
    TACTIC  RewriteWith(hyprewriterule, rewriterule, thm) IS
    (ALT
      (LAYOUT "rewrite %s" (1)
        (WHEN
          (LETHYPSUBSTSEL  _p (WITHSUBSTSEL hyprewriterule) thm)
          (LETCONCSUBSTSEL _p (WITHSUBSTSEL rewriterule) thm)
          (LETHYP          _p (hyprewriterule _p) thm)
          (LETCONC         _p (rewriterule _p) thm)))
      (FailSEL ("%s not applicable to text selection", thm)
               ("%s not applicable to hypothesis or conclusion", thm)))
    TACTIC Unfolding(thm) IS (UNFOLD rewrite thm)
    TACTIC Folding(thm)   IS (FOLD   rewritebackwards thm)
    TACTIC UnfoldWithIff(rule)
    (RewriteWith  hyprewrite rewrite rule)

    TACTIC FoldWithIff(rule)
    (RewriteWith  hyprewritebackwards rewritebackwards rule)

    MENU "Quantifier Rules" IS
        SEPARATOR
        ENTRY "å|§â"      IS UnfoldWithIff "å|§"
        ENTRY "å|§Å"      IS FoldWithIff   "å|§"
        ENTRY "Ñ|§â"      IS UnfoldWithIff "Ñ|§"
        ENTRY "Ñ|§Å"      IS FoldWithIff   "Ñ|§"
    END
